PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>


SELECT ?destinationName ?month (COUNT(?message) AS ?messageCount)
WHERE
{
    VALUES (?type) {(snvoc:Comment) (snvoc:Post)} .
    ?message a ?type .
    ?message snvoc:hasCreator ?person .
    ?message snvoc:creationDate ?creationDate .
    ?person snvoc:gender ?gender .
    ?person snvoc:birthday ?birthday .
    ?person snvoc:isLocatedIn/snvoc:isPartOf/foaf:name $country .

    FILTER NOT EXISTS { ?message snvoc:isLocatedIn/foaf:name $country }
    ?message snvoc:isLocatedIn/foaf:name ?destinationName
    BIND(MONTH(?creationDate) AS ?month)
}
GROUP BY ?destinationName ?month
ORDER BY DESC(?messageCount) ?destinationName ?month
LIMIT 100
