PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT (COUNT(*) AS ?count)
WHERE
{
    ?country a dbpedia-owl:Country .
    ?country foaf:name $country .
    ?a a snvoc:Person .
    ?b a snvoc:Person .
    ?c a snvoc:Person .
    ?a snvoc:id ?aId .
    ?b snvoc:id ?bId .
    ?c snvoc:id ?cId .
    ?a snvoc:isLocatedIn/snvoc:isPartOf ?country .
    ?b snvoc:isLocatedIn/snvoc:isPartOf ?country .
    ?c snvoc:isLocatedIn/snvoc:isPartOf ?country .

    {
        ?a snvoc:knows/snvoc:hasPerson ?b .
        ?a snvoc:id ?aId .
        ?b snvoc:id ?bId
        FILTER (?aId < ?bId)
    }
    UNION
    {
        ?b snvoc:knows/snvoc:hasPerson ?a .
        ?a snvoc:id ?aId .
        ?b snvoc:id ?bId
        FILTER (?aId < ?bId)
    }

    {
        ?b snvoc:knows/snvoc:hasPerson ?c .
        ?b snvoc:id ?bId .
        ?c snvoc:id ?cId
        FILTER (?bId < ?cId)
    }
    UNION
    {
        ?c snvoc:knows/snvoc:hasPerson ?b .
        ?b snvoc:id ?bId .
        ?c snvoc:id ?cId
        FILTER (?bId < ?cId)
    }

    {
        ?a snvoc:knows/snvoc:hasPerson ?c .
        ?a snvoc:id ?aId .
        ?c snvoc:id ?cId
        FILTER (?aId < ?cId)
    }
    UNION
    {
        ?c snvoc:knows/snvoc:hasPerson ?a .
        ?a snvoc:id ?aId .
        ?c snvoc:id ?cId
        FILTER (?aId < ?cId)
    }

    FILTER (?aId < ?bId && ?bId < ?cId)
}
GROUP BY ?country
